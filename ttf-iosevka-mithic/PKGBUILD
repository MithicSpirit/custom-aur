# Maintainer: MithicSpirit <rpc01234 at gmail dot com>
# Contributor: Andy Kluger <https://t.me/andykluger>
# Contributor: Markus Weimar <mail@markusweimar.de>

_pkgname=iosevka
pkgname=ttf-${_pkgname}-mithic
_commit_id=\
2f5a7b4e264b042d288f66f0bd42b26fe24964d0
_pkgname_id="${_pkgname}-${_commit_id}"
pkgver=11.2.0
pkgrel=2
pkgdesc='A custom monospace sans-serif typeface inspired by Pragmata Pro, M+, PF DIN Mono.'
arch=('any')
url='https://be5invis.github.io/Iosevka/'
license=('custom:OFL')
makedepends=('nodejs>=12.22.0' 'npm' 'ttfautohint')
source=("${_pkgname_id}.tar.gz::https://github.com/MithicSpirit/${_pkgname}/archive/${_commit_id}.tar.gz")
sha256sums=('65b48b7c21e0826c5cd967f9c4f70f52e883d062f353c45e833fcc253d4d202b')

build () {
  cd "${srcdir}/${_pkgname_id}"
  npm install
  npm run build -- ttf::iosevka-mithic
}

package () {
  cd "${srcdir}/${_pkgname_id}"
  install -d "${pkgdir}/usr/share/fonts/TTF/iosevka-mithic"
  install -m644 dist/iosevka-mithic/ttf/*.ttf "${pkgdir}/usr/share/fonts/TTF/iosevka-mithic/"
  install -d "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -m644 LICENSE.md "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
