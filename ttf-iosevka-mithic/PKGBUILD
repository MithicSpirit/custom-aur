# Maintainer: MithicSpirit <rpc01234 at gmail dot com>
# Contributor: Andy Kluger <https://t.me/andykluger>
# Contributor: Markus Weimar <mail@markusweimar.de>

_pkgname=iosevka
pkgname=ttf-${_pkgname}-mithic
_commit_id=\
829c6e495e81d1c43b0d512ce38dadd4609205eb
_pkgname_id="${_pkgname}-${_commit_id}"
pkgver=15.6.3.1
pkgrel=1
pkgdesc='A custom monospace sans-serif typeface inspired by Pragmata Pro, M+, PF DIN Mono.'
arch=('any')
url='https://be5invis.github.io/Iosevka/'
license=('custom:OFL')
makedepends=('nodejs>=12.22.0' 'npm' 'ttfautohint')
source=("${_pkgname_id}.tar.gz::https://github.com/MithicSpirit/${_pkgname}/archive/${_commit_id}.tar.gz" 
        get-makeflags.js)
sha256sums=('1d21dc2a9bded531621397175f00a886e616b024010956a4daeaa0e6b4ee41d1'
            '60d2f9eb2b2f5213da1eaad96fa0466954ac1be89cb02143d0035f6ea741c8a2')

build () {
    cd "${srcdir}/${_pkgname_id}"
    npm install

    local _makeflags="$(../get-makeflags.js)"
    [[ -z "$_makeflags" ]] && _makeflags="--jCmd=1"

    npm run build -- "$_makeflags" ttf::iosevka-mithic
}

package () {
    cd "${srcdir}/${_pkgname_id}"
    install -d "${pkgdir}/usr/share/fonts/TTF/iosevka-mithic"
    install -m644 dist/iosevka-mithic/ttf/*.ttf "${pkgdir}/usr/share/fonts/TTF/iosevka-mithic/"
    install -d "${pkgdir}/usr/share/licenses/${_pkgname}"
    install -m644 LICENSE.md "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
