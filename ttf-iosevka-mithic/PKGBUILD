# Maintainer: MithicSpirit <rpc01234 at gmail dot com>
# Contributor: Andy Kluger <https://t.me/andykluger>
# Contributor: Markus Weimar <mail@markusweimar.de>

_pkgname=iosevka
pkgname=ttf-${_pkgname}-mithic
_commit_id=\
208d80c2cd9a54b3ae239262d769a8a36343d519
_pkgname_id="${_pkgname}-${_commit_id}"
pkgver=14.0.0
pkgrel=1
pkgdesc='A custom monospace sans-serif typeface inspired by Pragmata Pro, M+, PF DIN Mono.'
arch=('any')
url='https://be5invis.github.io/Iosevka/'
license=('custom:OFL')
makedepends=('nodejs>=12.22.0' 'npm' 'ttfautohint')
source=("${_pkgname_id}.tar.gz::https://github.com/MithicSpirit/${_pkgname}/archive/${_commit_id}.tar.gz" 
        "get-makeflags.js")
sha256sums=('44280591d077d4b300eb6954110128caa8efde3e4f547c1397ff2e9a31f26ac2'
            '105a091376fcd985698abeeda74646322b600c2506c9c7406565699f49682945')

build () {
    cd "${srcdir}/${_pkgname_id}"
    npm install

    local _makeflags="$(../get-makeflags.js)"
    [[ -z "$_makeflags" ]] && _makeflags="--jCmd=1"

    npm run build -- "$_makeflags" ttf::iosevka-mithic
}

package () {
    cd "${srcdir}/${_pkgname_id}"
    install -d "${pkgdir}/usr/share/fonts/TTF/iosevka-mithic"
    install -m644 dist/iosevka-mithic/ttf/*.ttf "${pkgdir}/usr/share/fonts/TTF/iosevka-mithic/"
    install -d "${pkgdir}/usr/share/licenses/${_pkgname}"
    install -m644 LICENSE.md "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
