# Maintainer: MithicSpirit <rpc01234 at gmail dot com>
# Contributor: Andy Kluger <https://t.me/andykluger>
# Contributor: Markus Weimar <mail@markusweimar.de>

_pkgname=iosevka
pkgname=ttf-${_pkgname}-mithic
_commit_id=\
41dca85e7680f734c1d16d27c6e4fa524108c87d
_pkgname_id="${_pkgname}-${_commit_id}"
pkgver=11.2.4
pkgrel=5
pkgdesc='A custom monospace sans-serif typeface inspired by Pragmata Pro, M+, PF DIN Mono.'
arch=('any')
url='https://be5invis.github.io/Iosevka/'
license=('custom:OFL')
makedepends=('nodejs>=12.22.0' 'npm' 'ttfautohint')
source=("${_pkgname_id}.tar.gz::https://github.com/MithicSpirit/${_pkgname}/archive/${_commit_id}.tar.gz" 
        "get-makeflags.js")
sha256sums=('67e319bef82bd4fe1ea652768c74a70f20684fc053af349700b6ad804058d2a8'
            '105a091376fcd985698abeeda74646322b600c2506c9c7406565699f49682945')

build () {
    cd "${srcdir}/${_pkgname_id}"
    npm install

    local _makeflags="$(../get-makeflags.js)"
    [[ -z "$_makeflags" ]] && _makeflags="--jCmd=1"

    npm run build -- "$_makeflags" ttf::iosevka-mithic
}

package () {
    cd "${srcdir}/${_pkgname_id}"
    install -d "${pkgdir}/usr/share/fonts/TTF/iosevka-mithic"
    install -m644 dist/iosevka-mithic/ttf/*.ttf "${pkgdir}/usr/share/fonts/TTF/iosevka-mithic/"
    install -d "${pkgdir}/usr/share/licenses/${_pkgname}"
    install -m644 LICENSE.md "${pkgdir}/usr/share/licenses/${_pkgname}/"
}
