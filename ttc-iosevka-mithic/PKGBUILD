# Maintainer: MithicSpirit <rpc01234 at gmail dot com>
# Contributor: Andy Kluger <https://t.me/andykluger>
# Contributor: Markus Weimar <mail@markusweimar.de>

_pkgname=iosevka-mithic

_iosevka_version=27.3.5
_nerd_patcher_version=3.0.2

_iosevka="Iosevka-${_iosevka_version}"
_nerd_patcher="nerd-patcher-$_nerd_patcher_version"

pkgname=ttc-${_pkgname}
pkgver="${_iosevka_version}.nerd.${_nerd_patcher_version}.mithic.1"
pkgrel=1
provides=('ttf-font-nerd')
pkgdesc='A custom configuration of the Iosevka monospace font (with nerd symbols).'
arch=('any')
url='https://be5invis.github.io/Iosevka/'
license=('custom:OFL')
makedepends=('nodejs>=12.22.0' 'npm' 'ttfautohint' 'fontforge'
             'python-argparse')
source=("${_iosevka}.tar.gz::https://github.com/be5invis/Iosevka/archive/refs/tags/v${_iosevka_version}.tar.gz"
        "${_nerd_patcher}.zip::https://github.com/ryanoasis/nerd-fonts/releases/download/v${_nerd_patcher_version}/FontPatcher.zip"
        build-plans.toml
        get-makeflags.js)
sha512sums=('0cbd47671858b49baf67d389cac24171f9f80e7c684471208b3d29c56126512f9360bf309ffee8f8146113159b037b4f37fd5d1f763cca384fc2b3c1c4fdb66c'
            '2ec53b6353d36cb14f09f65090d74c038be1dfd26f1b53f4b2838618b4da7184ff76f69e24ffbad2197720f1351c03c9009f9e01d5b0f80c126f2e8b2def48c2'
            'b3a63f45597db37727c287c9a9ecc6c23ab9eacce599b4977ad74ec2770c438cc7b262ca5116a9e7aefd5c6c7d922323a67eb8ca832b809d87af2475fe0203d2'
            '167a2da7b6cb06b6b96b4ae204f46e67e4625052df065873158455b0dedc75d2c0080cd39be33da49a984febccff892b3e59e4626d41364e3a8b9865efbd725f')

prepare() {
	cd "${srcdir}"  # nerd patcher isn't in subdir
	sed -i 's/\( *\)def setup_font_names(.*):/&\n\1    return/' font-patcher

	cp build-plans.toml "${_iosevka}/private-build-plans.toml"
}

build () {
	cd "${srcdir}/${_iosevka}"
	npm install

	local _makeflags="$(../get-makeflags.js)"
	[[ -z "$_makeflags" ]] && _makeflags="--jCmd=1"

	npm run build -- "$_makeflags" super-ttc::iosevka-mithic

	cd "${srcdir}"  # nerd patcher isn't in subdir
	./font-patcher --mono --complete --careful \
		"${srcdir}/${_iosevka}/dist/.super-ttc/${_pkgname}.ttc"
}

package () {
	install -d "${pkgdir}/usr/share/fonts/TTF/"
	install -m644 "${srcdir}/Iosevka Mithic.ttc" \
		"${pkgdir}/usr/share/fonts/TTF/${_pkgname}.ttc"
	install -d "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -m644 "${srcdir}/${_iosevka}/LICENSE.md" \
		"${pkgdir}/usr/share/licenses/${pkgname}/"
}
