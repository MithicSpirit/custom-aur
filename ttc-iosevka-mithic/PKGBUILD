# Maintainer: MithicSpirit <rpc01234 at gmail dot com>
# Contributor: Andy Kluger <https://t.me/andykluger>
# Contributor: Markus Weimar <mail@markusweimar.de>

_pkgname=iosevka-mithic

_iosevka_version=28.1.0
_nerd_patcher_version=3.1.1
_config_version=3

_iosevka="Iosevka-${_iosevka_version}"
_nerd_patcher="nerd-patcher-${_nerd_patcher_version}"

pkgname="ttc-${_pkgname}"
pkgver="${_iosevka_version}.nerd.${_nerd_patcher_version}.mithic.${_config_version}"
pkgrel=1
provides=('ttf-font-nerd')
pkgdesc='A custom configuration of the Iosevka monospace font (with nerd symbols).'
arch=('any')
url='https://be5invis.github.io/Iosevka/'
license=('custom:OFL')
makedepends=('nodejs>=12.22.0' 'npm' 'ttfautohint' 'fontforge')
source=("${_iosevka}.tar.gz::https://github.com/be5invis/Iosevka/archive/refs/tags/v${_iosevka_version}.tar.gz"
        "${_nerd_patcher}.zip::https://github.com/ryanoasis/nerd-fonts/releases/download/v${_nerd_patcher_version}/FontPatcher.zip"
        build-plans.toml
        get-makeflags.js)
sha512sums=('fce9813cb8590096b602b612dd544e305af140a90c89abd31fbb90aa7ceffadcaf9a818e8e82fe368d968fad1bc8a6075da6da94fe61035688ffcae4c4495b84'
            '83c323a501372609b063826bf1749469676d1e2bb13f5ff9786f9e5941dd1bd8f68567a829b5b7b1ad8c6e4afe956a4a0f866e243101fde6b9667206351e8c43'
            '2eff7a2b2229504bdf0b526b0617c266ba7b78e472f52061c37b9e74ebfa083f83a3e38eaf2cdacf8120fb513ba86fc16359b37cc95b1aab973bf16393ebfe57'
            '167a2da7b6cb06b6b96b4ae204f46e67e4625052df065873158455b0dedc75d2c0080cd39be33da49a984febccff892b3e59e4626d41364e3a8b9865efbd725f')

prepare() {
	cd "${srcdir}"  # nerd patcher isn't in subdir
	sed -i 's/\( *\)def setup_font_names(.*):/&\n\1    return/' font-patcher

	cp build-plans.toml "${_iosevka}/private-build-plans.toml"
}

build () {
	cd "${srcdir}/${_iosevka}"
	npm install

	local _makeflags="$(../get-makeflags.js)"
	[[ -z "$_makeflags" ]] && _makeflags="--jCmd=1"

	npm run build -- "$_makeflags" super-ttc::IosevkaMithic

	cd "${srcdir}"  # nerd patcher isn't in subdir
	./font-patcher --mono --complete --careful \
		"${srcdir}/${_iosevka}/dist/.super-ttc/IosevkaMithic.ttc"
}

package () {
	install -d "${pkgdir}/usr/share/fonts/TTF/"
	install -m644 "${srcdir}/Iosevka Mithic.ttc" \
		"${pkgdir}/usr/share/fonts/TTF/${_pkgname}.ttc"
	install -d "${pkgdir}/usr/share/licenses/${pkgname}/"
	install -m644 "${srcdir}/${_iosevka}/LICENSE.md" \
		"${pkgdir}/usr/share/licenses/${pkgname}/"
}
