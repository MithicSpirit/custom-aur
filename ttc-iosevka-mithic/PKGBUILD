# Maintainer: MithicSpirit <rpc01234 at gmail dot com>
# Contributor: Andy Kluger <https://t.me/andykluger>
# Contributor: Markus Weimar <mail@markusweimar.de>

_pkgname=iosevka
pkgname=ttc-${_pkgname}-mithic
_commit_id=\
32f16dfa982bca5b00a19f877108e04933d85cad
_pkgname_id="${_pkgname}-${_commit_id}"
pkgver=16.8.2.0
pkgrel=1
replaces=("ttf-${pkgname##ttc-}")
pkgdesc='A custom monospace sans-serif typeface inspired by Pragmata Pro, M+, PF DIN Mono.'
arch=('any')
url='https://be5invis.github.io/Iosevka/'
license=('custom:OFL')
makedepends=('nodejs>=12.22.0' 'npm' 'ttfautohint')
source=("${_pkgname_id}.tar.gz::https://github.com/MithicSpirit/${_pkgname}/archive/${_commit_id}.tar.gz" 
        get-makeflags.js)
sha256sums=('971e9739a6490ddaa394a047b746885eb97ac96bcac5cb24554e7b27a2cfefd6'
            '60d2f9eb2b2f5213da1eaad96fa0466954ac1be89cb02143d0035f6ea741c8a2')

build () {
    cd "${srcdir}/${_pkgname_id}"
    npm install

    local _makeflags="$(../get-makeflags.js)"
    [[ -z "$_makeflags" ]] && _makeflags="--jCmd=1"

    npm run build -- "$_makeflags" super-ttc::iosevka-mithic
}

package () {
    cd "${srcdir}/${_pkgname_id}"
    install -d "${pkgdir}/usr/share/fonts/TTF/"
    install -m644 dist/.super-ttc/*.ttc "${pkgdir}/usr/share/fonts/TTF/"
    install -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/"
}
