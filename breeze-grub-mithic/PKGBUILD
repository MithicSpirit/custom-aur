# Maintainer: MithicSpirit <rpc01234 at gmail dot com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

_pkgname=breeze-grub
pkgname="$_pkgname-mithic"
pkgver=5.23.3
pkgrel=1
pkgdesc='Breeze theme for GRUB'
arch=(any)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(grub)
makedepends=()
provides=(breeze-grub)
conflicts=(breeze-grub)
source=("https://download.kde.org/stable/plasma/$pkgver/$_pkgname-$pkgver.tar.xz"{,.sig}
        theme.txt.diff)
sha256sums=('fe033eb5b3630849a12335c00b840bea6e84b35f00c103ed27e9b569019790db'
            'SKIP'
            '9b1b6904272baef13f40b39902256ff1077d1ed434f12d9aea27a39868a25763')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  patch -d "$_pkgname-$pkgver/breeze" < theme.txt.diff
}

package() {
  install -d "$pkgdir/usr/share/grub/themes"
  cp -r "$_pkgname-$pkgver/breeze" "$pkgdir/usr/share/grub/themes"
}
